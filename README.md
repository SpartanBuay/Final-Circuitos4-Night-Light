# Final-Circuitos4-Night-Light
Ulatina, proyecto final circuitos 4/ Luz de noche manipulable 

# <p align="center">  üí°Luz de Noche Manipulableüì±  <p align="center"> Proyectofinal de Circuitos y Sistemas IV

Para el proyecto final de Circuitos y Sistemas IV se decidi√≥ crear una luz de noche
(un prototipo, para ser m√°s espec√≠ficos) que se pueda manipular por wifi y
f√≠sicamente con potenci√≥metro(s), tanto en color como en intensidad y que ayude a
iluminar en la noche sin perturbar mucho la oscuridad.
En esta documentaci√≥n se presentar√°n los detalles b√°sicos de sus partes, c√≥digo y
funcionamiento.

> ### Para ello empecemos con los componentes del prototipo‚Ä¶
------------------------------------------------------------

Inicialmente se pensaba en utilizar fotoresistores para crear una luz de noche que
responda autom√°ticamente con la luz que le rodea pero, debido a circunstancias
fuera de nuestro control se opt√≥ por utilizar otros materiales y darle un
funcionamiento diferente que explicara m√°s adelante.

* Para el prototipo se utiliz√≥:üìù
  - ESP32
  - 2 LEDs rgb c√°todo com√∫n
  - 6 resistencias de 330 ohms
  - 4 resistencias de 220 ohms
  - Potenci√≥metro de 10k ohms
  - cables
  - fuente de alimentaci√≥n externa
  
### <p align="center"> üì∑Imagenes del proyecto üì∑
<p align="center">
¬°<img src="https://user-images.githubusercontent.com/89326280/130339721-e59b5aa0-0cc8-408f-9917-c2e0ffb471e9.png" width="300">
<img src="https://user-images.githubusercontent.com/89326280/130339940-2f6e72d1-d438-494b-9502-c10f5f81e960.png" width="300">
<img src="https://user-images.githubusercontent.com/89326280/130340057-90923919-48f7-4e1c-84b2-8a096c32d3e7.png" width="275">
  
  ### **[Video Del Funcionamiento](https://drive.google.com/file/d/1mtfiSnPACxtCBxi_FRVO39Qx-CYtJV2Y/view?usp=sharing).** üîó
  
## **Funcionamiento**üîß 
 En el c√≥digo se est√°n implementando tres fundamentos del microcontrolador a la vez:
  - Generaci√≥n, manipulaci√≥n y uso de una se√±al PMW.
    En el c√≥digo utilizamos 3 canales, un canal por color, para controlar la combinaciones de
    colores a dar en el led rgb. Inicialmente se definen variables globales para cada uno de los
    canales definiendo par√°metros como la frecuencia del pulso y la resoluci√≥n de la se√±al (la
    cual fue dada en 10 bits, en otras palabras el pulso puede ser emitido en 1024 intensidades
    diferentes las cuales son la divisi√≥n en partes iguales del voltaje m√°ximo de salida.
  
 - Conversi√≥n ADC input en resoluci√≥n de 10 bits.
    Para el control del nivel de brillo, el microcontrolador recibe una se√±al an√°loga y la convierte
    en digital asign√°ndole un valor a la lectura entre 0 y 1023 (10 bits de resoluci√≥n, como la
    se√±al PMW), este valor es asignado en una variable para luego ser comparado en una serie
    de rangos y definir a qu√© porcentaje del valor obtenido para cada color realmente vamos a
    mandar la se√±al pmw al led.
    Por ejemplo, si el rango le√≠do del potenciometro cae en 50% y el usuario aplica en la p√°gina
    un valor de 1000 para el color rojo y 0 para los otros colores, en lugar de mandar una se√±al
    pmw al canal del led rojo de 1000 directamente, antes el valor se multiplica por la variable
    ‚Äúbrillo‚Äù la cual para este caso tendr√° un valor de 0.5 d√°ndole un valor final a la se√±al de 500.
    Esto permite mantener la combinaci√≥n de colores entre los tres canales relativamente
    consistente mientras que se da la oportunidad de controlar la luminosidad final sin perder la
    combinaci√≥n de colores deseada
  
  - Uso de el microcontrolador como web server (gracias a la librer√≠a WiFi.h para esp32)
    Para poder poner una p√°gina web c√≥mo interfaz y controlar los colores y el encendido y
    apagado de la luz nocturna primero se necesita montar un servidor. La librer√≠a de la esp32
    trae algunos ejemplos de la implementaci√≥n y uso de la librer√≠a WiFi.h tanto como servidor,
    como host. Una vez se da el modo de operaci√≥n creamos dos variables para el nombre de
    la red a conectar y la contrase√±a de la red (en este caso las variables se llaman ssid y
    password respectivamente.
    Para este caso se ha configurado de manera previa una direcci√≥n est√°tica en el router y se
    le han asignado estos valores a el microcontrolador en la parte del setup (ip, mascara
    subred y gateway), en caso de no querer hacer esto simplemente se omite la linea
    ‚ÄúWiFi.begin(ip, m√°scara de subred, gateway);‚Äù en la parte del setup y WiFi.begin con el
    nombre de red y contrase√±a se conecta a la red, luego el router le dar√° una direcci√≥n por
    dhcp (si tiene la configuracion predeterminada) y esta direccion ip sera impresa en el serial,
    la cual ser√° la direcci√≥n a ingresar para dar instrucciones a la esp32 a trav√©s de la p√°gina
    web en la red local.
    Una vez la conexi√≥n es establecida y la negociaci√≥n http es realizada, podemos mandar
    html a el cliente y obtener cambios realizados en la direcci√≥n los cuales se pueden tomar
    como condiciones para instrucciones en el mictrocontrolador, con esto podemos saber
    cuando el cliente en la pagina mueve el valor de una de las barras de ‚Äúnivel de color‚Äù o Hue,
    presiona el boton de encendido o apagado y proceder con estas instrucciones.

## ‚ö°Esp32 - devkit-v1‚ö°
  Para el prototipo se escogi√≥ utilizar el microcontrolador ESP32-DevKit-V1 de 30 Pines con
  Wifi y Bluetooth.
  
> ### Algunos de los datos m√°s relevantes del microcontrolador sacado de su documentaci√≥n:
  - El ESP32 DEVKIT V1 es un microcontrolador de bajo costo y consumo de energ√≠a,
    cuenta con tecnolog√≠a Wi-Fi y Bluetooth de modo dual integrada que permite
    controlar todo tipo de sensores, m√≥dulos y actuadores; es el sucesor del
    microcontrolador ESP8266.
  - Permite generar proyectos de Internet de las cosas ‚ÄúIoT‚Äù de forma eficiente y
    econ√≥mica, ya que integra internamente una gran cantidad de perif√©ricos incluyendo:
    sensores t√°ctiles capacitivos, sensor de efecto Hall, amplificadores de bajo ruido,
    interfaz para tarjeta SD, Ethernet, SPI de alta velocidad, UART, I2S e I2C. Tiene un
    CPU de dos n√∫cleos de hasta 240Mhz que se pueden controlar independientemente.
  
  - Caracter√≠sticas espec√≠ficas:
    - Serie: ESP32 DEVKIT V1
    - Chip USB-Serial: CP2102
    - Voltaje de Alimentaci√≥n (USB): 5V DC
    - Voltaje de Entradas/Salidas: 3.3V DC
    - Consumo de energ√≠a de 5ŒºA en modo de suspensi√≥n
    - Pines Digitales GPIO: 24 (Algunos pines solo como entrada)
    - Conversor Anal√≥gico Digital:
      - Dos ADC de 12bits tipo SAR, soporta mediciones en hasta 18
        canales, algunos pines soporta un amplificador con ganancia
        programable
    - Antena en PCB
    - Tipo: M√≥dulo Wifi + Bluetooth
    - Wifi: 802.11 b/g/n/e/i (802.11n @ 2.4 GHz hasta 150 Mbit/s)
    - Bluetooth: 4.2 BR/EDR BLE Modo de control dual
    - CPU principal: Tensilica Xtensa 32-bit LX6
    - Memoria: 448 KByte ROM, 520 KByte SRAM, 6 KByte SRAM en RTC y QSPI
      admite m√∫ltiples chips flash /SRAM
    - Procesador secundario: Permite hacer operaciones b√°sica en modo de ultra
      bajo consumo
    - Desempe√±o: Hasta 600 DMIPS
    - Frecuencia de Reloj: hasta 240Mhz
    - Seguridad: IEEE 802.11, incluyendo WFA, WPA/WPA2 y WAPI
    - Criptograf√≠a acelerada por hardware: AES, SHA-2, RSA, criptograf√≠a de curva
      el√≠ptica (ECC), generador de n√∫meros aleatorios (RNG)
  
### **ESP32 [Datasheet](https://www.espressif.com/sites/default/files/documentation/esp32_datasheet_en.pdf).** üîó
  
## DiagramaüìÑ
  <p align="center">
  <img src="https://user-images.githubusercontent.com/89326280/130342282-255ca9ba-5025-4b6e-bfcf-035ed4f835b8.png" width="350">
  
## C√≥digo üë®‚Äçüíª
     #include <WiFi.h>

    //-------------------------------------------------Inicio P√°gina Web----------------------------------------------------------------------

    //Parte que siempre ser√° impresa, podemos meterlo todo en una variable para facilitar lectura en el void loop
    String Head = 
    "<!DOCTYPE html>"
    "<html>"
    "<head>"
      "<meta name=\"viewport\" content=\"width=device-width, initial-scale=1\">"
      "<link rel=\"icon\" href=\"data:,\">"
      "<style>"
        "body {"
          "text-align: center;"
          "font-family: \"Trebuchet MS\", Arial;"
          "margin-left:auto;"
          "margin-right:auto;"
          "background-color: rgb(67, 90, 100);"
          "color: whitesmoke;"
        "}"
        ".slider {"
          "width: 70%;"
        "}"

        ".red{"
         " color: orangered;"

        "}"

        ".blue{"
          "color: lightblue;"

        "}"

        ".green{"
         " color: lightgreen;"

        "}"
        ".on{"
          "background-color: green;"
          "color: whitesmoke;"
          "width: 90px;"
          "height: 90px;"
        "}"
        ".off{"
          "background-color: darkslategray;"
          "color: red;"
          "width: 90px;"
          "height: 90px;"
        "}"

      "</style>"
      "<script src=\"https://ajax.googleapis.com/ajax/libs/jquery/3.3.1/jquery.min.js\"></script>"
    "</head>"
    "<body>"
      "<h1>Luz Nocturna</h1>";


    //---------------------------Botones en la Web--------------------
    //Boton de Encendido
    String On = "<p><a href=\"/on\"><button class=\"on\">ON</button></a></p>";

    //Boton de Apagado
    String Off = "<p><a href=\"/off\"><button class=\"off\">OFF</button></a></p>";


    //--------------------------------------------------------Parte para web server-----------------------------------------------------------
    const char* ssid = "NOMBRE DE TU RED WIFI";
    const char* password = "CONTRASE√ëA DE TU RED WIFI";

    WiFiServer server(80);

    //static values for dhcp, solo si se ha configurado en el router de manera previa
    /*
    IPAddress ip(192,168,0,69);
    IPAddress gateway(192,168,0,1);
    IPAddress subnet(255,255,255,0);
    */

    //Para almacenar el http request
    String header;



    //El valor con el que se controlara cada color
    String valHue = String(5);
    //strings para "decodificar" el valor de los sliders, ya que vienen de un javascript
    int pos1;
    int pos2;




    // Current time
    unsigned long currentTime = millis();
    // Previous time
    unsigned long previousTime = 0; 
    // Define timeout time in milliseconds (example: 2000ms = 2s)
    const long timeoutTime = 2000;


    //----------------------------------------------------------Parte para los leds-----------------------------------------------------------
    //LED pin para 2
    const int ledpin1r = 25;
    const int ledpin1g = 26;
    const int ledpin1b = 27;

    const int ledpin2r = 14;
    const int ledpin2g = 12;
    const int ledpin2b = 13; 

    //-------------------------------Para el Boton (el estado del ciclo anterior a el que se est√° ejecutando)---------------------------------
    String prevstat = "off";


    //--------------------------------------------------Frecuencias de Pmw y eso---------------------------------------------------------------

    //Channel for red color
    const int freqR = 5000; //5000Hz
    const int ledChannR = 0;
    const int resolutionR = 10;

    //Channel for green color
    const int freqG = 5000; //5000Hz
    const int ledChannG = 1;
    const int resolutionG = 10;

    //Channel for blue color
    const int freqB = 5000; //5000Hz
    const int ledChannB = 2;
    const int resolutionB = 10;



    //---------------------------------------------------------------------- Para el ADC ------------------------------------------------------
    const int potPin = 36;      //seleccionar el pin de entrada para el pot
    int potValue;       
    float brillo;

    //Variables para "memorizar" el ultimo cambio al color, y cambiar la intensidad sin depender de un get del web
    int MemR = 0;
    int MemG = 0;
    int MemB = 0;


    //--------------------------------------------------------------------------Loop Setup---------------------------------------------------------------------------------------------------------
    void setup() {
    //PMW setup

      //LED PMW func
      ledcSetup(ledChannR, freqR, resolutionR);
      ledcSetup(ledChannG, freqG, resolutionG);
      ledcSetup(ledChannB, freqB, resolutionB);
      //GPIO attachment
      ledcAttachPin(ledpin1r, ledChannR);
      ledcAttachPin(ledpin2r, ledChannR);
      ledcAttachPin(ledpin1g, ledChannG);
      ledcAttachPin(ledpin2g, ledChannG);
      ledcAttachPin(ledpin1b, ledChannB);
      ledcAttachPin(ledpin2b, ledChannB);


    //web setup  
      Serial.begin(115200);
      // Connect to Wi-Fi network with SSID and password
      Serial.print("Connecting to ");
      Serial.println(ssid);

    //Usar el Wifi.config solo si se ha seteado direcci√≥n IP fija en el router, de caso contrario no utilizar
    //este comando y omitir las variables ip, gateway y subnet; el router dar√° una ip por dhcp y esta ser√° impresa en serial  
    //  WiFi.config(ip, gateway, subnet);

      WiFi.begin(ssid, password);
      while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.println("Me estoy tratando de conectar :'( ");
      }

      // Print local IP address and start web server
      Serial.println("");
      Serial.println("WiFi connected. :D");
      Serial.println("IP address: ");
      Serial.println(WiFi.localIP());
      server.begin();


    //ADC setup
       analogReadResolution(10);
       analogSetClockDiv(1);
    }

    void loop() {
    //Lo primero que hacemos fuera de la evaluaci√≥n de la p√°gina web y http request 
    //es obtener nuestro valor de brillo para este ciclo (le√≠do desde el potenciometro)
    potValue = analogRead(potPin);

    if (potValue < 256){
     brillo = 0.25;
      }

    else if (potValue < 512 && potValue >= 256){
     brillo = 0.5;
      }

    else if (potValue < 768 && potValue >= 512){
     brillo = 0.75;
      }

    else if (potValue < 1024 && potValue >= 768){
     brillo = 1;
      }

    //-----------------------------parte del http request, que no se altera-------------------------------------------
       WiFiClient client = server.available();   // Listen for incoming clients

      if (client) {                             // If a new client connects,
        currentTime = millis();
        previousTime = currentTime;
        Serial.println("New Client.");          // print a message out in the serial port
        String currentLine = "";                // make a String to hold incoming data from the client
        while (client.connected() && currentTime - previousTime <= timeoutTime) {  // loop while the client's connected
          currentTime = millis();
          if (client.available()) {             // if there's bytes to read from the client,
            char c = client.read();             // read a byte, then
            Serial.write(c);                    // print it out the serial monitor
            header += c;
            if (c == '\n') {                    // if the byte is a newline character
              // if the current line is blank, you got two newline characters in a row.
              // that's the end of the client HTTP request, so send a response:
              if (currentLine.length() == 0) {
                // HTTP headers always start with a response code (e.g. HTTP/1.1 200 OK)
                // and a content-type so the client knows what's coming, then a blank line:
                client.println("HTTP/1.1 200 OK");
                client.println("Content-type:text/html");
                client.println("Connection: close");
                client.println();



    //-------------------------------------------------Hacemos Display de La P√°gina Web---------------------------------------------------------

                //Imprimimos la primera parte de la p√°gina (solo mostraremos el bot√≥n de Encendido al acceder)
                client.println(Head);

                //Al "encender" haremos 2 ciclos, el primero encenderemos de manera manual para coincidir con lo que se mostrar√° en la p√°gina en el segundo ciclo
                //esta condici√≥n se cumple una sola vez al encender la luz ya que "recordaremos" el estado previo de el ciclo anterior
                if(header.indexOf("GET /on") >= 0 && prevstat == "off"){

                  //Grabamos en la memoria la condici√≥n m√°xima de cada valor para encender todos por predeterminado al encender los leds
                  MemR = 1023;
                  MemG = 1023;
                  MemB = 1023;

                  prevstat = "on";          
                  }

                //Desde El segundo Ciclo en adelante esta ser√° la condici√≥n que se cumpla hasta apagar el led, el cual regresar√° a el estado inicial de la p√°gina
                if(header.indexOf("GET /on") >= 0 && prevstat == "on"){
                  client.println(Off);

                  //Imprimimos los sliders en el siguiente orden: Rojo, Verde, Azul

                  //Slider Rojo
                  client.println("<p class=\"red\">Hue Rojo: </p><input type=\"range\" min=\"0\" max=\"1023\" value = \"1023\" class=\"slider\" id=\"red\" onchange=\"red(this.value)\"/>"); 
                  client.println("<script>var re = document.getElementById(\"red\"); re.innerHTML = slider.value;");
                  client.println("slider.oninput = function() {slider.value = this.value;red.innerHTML = this.value;}");
                  client.println("$.ajaxSetup({timeout:1000});function red(pos) {");
                  client.println("$.get(\"/on/?reeed=\" + pos + \"&\");{Connection: close};}</script>");

                  //Slider Verde
                  client.println("<p class=\"green\">Hue Verde: </p><input type=\"range\" min=\"0\" max=\"1023\" value = \"1023\" class=\"slider\" id=\"green\" onchange=\"green(this.value)\"/>"); 
                  client.println("<script>var gren = document.getElementById(\"green\"); gren.innerHTML = slider.value;");
                  client.println("slider.oninput = function() {slider.value = this.value;green.innerHTML = this.value;}");
                  client.println("$.ajaxSetup({timeout:1000});function green(pos) {");
                  client.println("$.get(\"/on/?green=\" + pos + \"&\");{Connection: close};}</script>");

                  //Slider Azul
                  client.println("<p class=\"blue\">Hue Azul: </p><input type=\"range\" min=\"0\" max=\"1023\" value = \"1023\" class=\"slider\" id=\"blue\" onchange=\"blue(this.value)\"/>"); 
                  client.println("<script>var blu = document.getElementById(\"blue\");blu.innerHTML = slider.value;");
                  client.println("slider.oninput = function() {slider.value = this.value;blue.innerHTML = this.value;}");
                  client.println("$.ajaxSetup({timeout:1000});function blue(pos) {");
                  client.println("$.get(\"/on/?blues=\" + pos + \"&\");{Connection: close};}</script>");

                  client.println("</body></html>");

                  //Para obtener el valor del √∫ltimo cambio realizado al Slider
                  if(header.indexOf("GET /on/?reeed=") >= 0){
                    Serial.println("Rojo Get");
                    pos1 = header.indexOf('=');
                    pos2 = header.indexOf('&');

                    valHue = header.substring(pos1 + 1, pos2);
                    MemR = valHue.toInt(); 
                  }

                  if(header.indexOf("GET /on/?blues=") >= 0){
                    Serial.println("Azul Get");
                    pos1 = header.indexOf('=');
                    pos2 = header.indexOf('&');

                    valHue = header.substring(pos1 + 1, pos2);
                    MemB = valHue.toInt();
                  }

                  if(header.indexOf("GET /on/?green=") >= 0){
                    Serial.println("Verde Get");
                    pos1 = header.indexOf('=');
                    pos2 = header.indexOf('&');

                    valHue = header.substring(pos1 + 1, pos2);
                    MemG = valHue.toInt();
                  }                
                }


                //Condici√≥n sucede si se presiona el boton de apagar o se va a cualquiera otra direcci√≥n fuera de 
                //la direccion http://192.168.0.69/on..., procede a apagar
                else{
                  client.println(On);
                  MemR = 0;
                  MemG = 0;
                  MemB = 0;
                  prevstat = "off";
                }

              client.println();
              break;
              }

              else{
                currentLine = "";
              }

            }

            else if(c != '\r'){
              currentLine += c;
              }
            }       
          }
        // Clear the header variable
        header = "";
        // Close the connection
        client.stop();
        Serial.println("Client disconnected.");
        Serial.println("");         
        }   

      //Los valores del ciclo son guardados en las variables de memoria MemR/MemG/MemB (colores R, G y B)
      //podemos encender la luz con la intensidad le√≠da en este ciclo y colores del ciclo anterior
      ledcWrite(ledChannR, MemR * brillo);
      ledcWrite(ledChannG, MemG * brillo);
      ledcWrite(ledChannB, MemB * brillo);

    }
  
